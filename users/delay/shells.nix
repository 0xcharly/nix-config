{
  lib,
  pkgs,
  ...
} @ args: let
  inherit (pkgs.stdenv) isLinux;

  shellAliases = shell:
    {
      # Shortcut to setup a nix-shell with `shell`. This lets you do something
      # like `nixsh -p go` to get an environment with Go but use `shell` along
      # with it.
      nixsh = "nix-shell --run ${shell}";
      devsh = "nix develop --command ${shell}";
    }
    // (lib.optionalAttrs isLinux {
      # For consistency with macOS.
      pbcopy = lib.getExe pkgs.xclip;
      pbpaste = "${lib.getExe pkgs.xclip} -o";
    });
in {
  programs.bash.enable = true;
  programs.htop.enable = true;

  # `cat` replacement.
  programs.bat = {
    enable = true;
    config = {theme = "base16";};
  };

  # `find` replacement.
  programs.fd.enable = true;

  # `grep` replacement.
  programs.ripgrep.enable = true;

  # GitHub command-line integration.
  programs.gh.enable = true;

  # `ls` replacement.
  programs.eza = {
    enable = true;
    enableFishIntegration = true;
    enableZshIntegration = true;
  };

  programs.direnv = {
    enable = true;
    # enableFishIntegration = true; # read-only; always enabled.
    enableZshIntegration = true;
    nix-direnv.enable = true;
    config.whitelist.prefix = ["~/code/"];
  };

  programs.fzf = {
    enable = true;
    enableFishIntegration = true;
    enableZshIntegration = true;
    # Catppuccin theme for FzF. https://github.com/catppuccin/fzf
    colors = {
      bg = "#1e1e2e";
      "bg+" = "#313244";
      fg = "#cdd6f4";
      "fg+" = "#cdd6f4";
      header = "#f38ba8";
      hl = "#f38ba8";
      "hl+" = "#f38ba8";
      info = "#cba6f7";
      marker = "#f5e0dc";
      pointer = "#f5e0dc";
      prompt = "#cba6f7";
      spinner = "#f5e0dc";
    };
  };

  programs.zsh = {
    enable = true;
    defaultKeymap = "viins";
    history = {
      ignoreDups = true;
      ignoreAllDups = true;
    };
    initExtra = lib.strings.concatStringsSep "\n" [
      ''
        setopt HIST_REDUCE_BLANKS
        setopt INC_APPEND_HISTORY # Use `fc -RI` to reload history.
      ''
      # Our own ZSH plugins.
      ''
        source ${pkgs.tmux-open-git-repository-zsh}/share/zsh/plugins/tmux-open-git-repository/tmux-open-git-repository.plugin.zsh
      ''
      (builtins.readFile ./rprompt.zsh)
      (lib.optionalString isLinux "eval $(${lib.getExe pkgs.keychain} --eval --nogui --quiet)")
    ];
    localVariables = {
      PS1 = "%B%F{blue}îš‘_%f%b ";
    };
    shellAliases = shellAliases (lib.getExe pkgs.zsh);
    syntaxHighlighting.enable = true;
  };

  programs.fish = {
    enable = true;
    interactiveShellInit = lib.strings.concatStringsSep "\n" [
      (builtins.readFile ./config.fish)
      "set -g SHELL ${lib.getExe pkgs.fish}"
      (lib.optionalString isLinux "eval (${lib.getExe pkgs.keychain} --eval --nogui --quiet)")
    ];

    functions.fish_mode_prompt = ""; # Disable prompt vi mode reporting.
    shellAliases = shellAliases (lib.getExe pkgs.fish);
  };

  home.packages = [
    pkgs.fishPlugins.done
    pkgs.fishPlugins.fzf
    pkgs.fishPlugins.transient-fish
    pkgs.open-local-repository-fish
  ];
}
